
CREATE  PROCEDURE USP_CBS_Global_Get_CustomerInformation
(
	@CUSTOMER_CODE VARCHAR(20)
)
AS
BEGIN
	SET NOCOUNT ON;

	DECLARE @FULL_NAME VARCHAR(1000) = NULL;
	DECLARE @IS_CUSTOMER_INDIVIDUAL BIT = 1;
	SELECT TOP 1 @IS_CUSTOMER_INDIVIDUAL = CASE WHEN CUSTOMER_LEGAL_SCOPE = 1 THEN 1 ELSE 0 END FROM CUSTOMER_MAST WHERE CUSTOMER_CODE = @CUSTOMER_CODE;

	-- CUSTOMER MASTER
    SELECT 
	    CUSTOMER_CODE,
	    ISNULL(@FULL_NAME, FULL_NAME) FULL_NAME,
	    ISNULL(MOBILE_NO, '') MOBILE_NO,
	    CASE WHEN @IS_CUSTOMER_INDIVIDUAL = 1 THEN ISNULL(CONVERT(VARCHAR(10),BIRTH_DATE,121), '') ELSE ISNULL(CONVERT(VARCHAR(10),REGISTRATION_DATE,121), '') END BIRTH_DATE,
		CASE WHEN @IS_CUSTOMER_INDIVIDUAL = 1 THEN ISNULL(DT.NEP_DATE, '') ELSE ISNULL(DT1.NEP_DATE, '') END BIRTH_DATE_NEP,
		@IS_CUSTOMER_INDIVIDUAL IS_INDIVIDUAL_CUSTOMER
    FROM CUSTOMER_MAST CM
	LEFT JOIN DATE_TABLE DT ON DT.ENG_DATE=CM.BIRTH_DATE
	LEFT JOIN DATE_TABLE DT1 ON DT1.ENG_DATE=CM.REGISTRATION_DATE

    WHERE CUSTOMER_CODE = @CUSTOMER_CODE
	    AND [STATUS] = 1

    -- CUSTOMER ADDRESS
    SELECT 
	    CA.ADDRESS_TYPE,
	    ISNULL(LB.LOCAL_BODY_NAME, '') LOCAL_BODY_NAME,
	    ISNULL(CA.TOLE_NAME, '') TOLE_NAME,
	    ISNULL(CA.WARD_NO, '') WARD_NO,
        CA.LONGITUDE,
        CA.LATITUDE,
		CA.OLD_ADDRESS
    FROM CUSTOMER_ADDRESS CA
	    LEFT JOIN LOCAL_BODY LB ON CA.LOCAL_BODY_ID = LB.TRAN_ID
    WHERE CUSTOMER_CODE = @CUSTOMER_CODE
	    AND CA.[STATUS] = 1

    -- CUSTOMER IDENTITY
    SELECT
	    ISNULL((
		    CASE WHEN IT.ISSUE_OFFICE_TYPE_ID = 1 THEN (SELECT TOP 1 DISTRICT_NAME FROM DISTRICT WHERE DISTRICT_CODE = CI.ISSUE_AUTHORITY_OFFICE_TYPE_CODE) -- District Office
			    WHEN IT.ISSUE_OFFICE_TYPE_ID = 2 THEN (SELECT TOP 1 LOCAL_BODY_NAME FROM LOCAL_BODY WHERE TRAN_ID = CI.ISSUE_AUTHORITY_OFFICE_TYPE_CODE) -- Local body office
			    WHEN IT.ISSUE_OFFICE_TYPE_ID = 3 THEN '' -- Federal Government Office
			    WHEN IT.ISSUE_OFFICE_TYPE_ID = 4 THEN (SELECT TOP 1 STATE_NAME FROM [STATE] WHERE STATE_CODE = CI.ISSUE_AUTHORITY_OFFICE_TYPE_CODE) -- Province Government office
		    ELSE CI.ISSUE_AUTHORITY_OFFICE_TYPE_CODE END
	    ), '') ISSUE_AUTHORITY_OFFICE_TYPE_NAME,
        ISNULL(IT.IDENTITY_TYPE_NAME, '') IDENTITY_TYPE_NAME, 
	    ISNULL(CI.ISSUE_AUTHORITY_OFFICE_TYPE_CODE, '') ISSUE_AUTHORITY_OFFICE_TYPE_CODE,
	    ISNULL(CI.ISSUE_AUTHORITY_OFFICE_NAME, '') ISSUE_AUTHORITY_OFFICE_NAME,
	    ISNULL(CI.IDENTITY_NO, '') IDENTITY_NO,
        CI.TRAN_ID
    FROM CUSTOMER_IDENTITY CI
	    INNER JOIN IDENTITY_TYPE IT ON CI.IDENTITY_TYPE_ID = IT.TRAN_ID
    WHERE CI.CUSTOMER_CODE = @CUSTOMER_CODE
	    AND CI.[STATUS] = 1

    -- CUSTOMER PHOTO
    SELECT 
	    ISNULL(CP.DOC_GUID, '') DOC_GUID,
	    ISNULL(FILE_EXTENSION, '') FILE_EXTENSION,
	    ISNULL(MIME_TYPE, '') MIME_TYPE,
		DS.DOC_FILE
    FROM CUSTOMER_PHOTO CP
	INNER JOIN DOC_STORE DS ON DS.DOC_GUID=CP.DOC_GUID
    WHERE CUSTOMER_CODE = @CUSTOMER_CODE
	    AND [STATUS] = 1

    -- CUSTOMER FAMILY
    SELECT 
	    ISNULL(R.RELATION_NAME, '') RELATION_NAME,
	    ISNULL(CFT.FULL_NAME, '') FULL_NAME,
		ISNULL(CONVERT(VARCHAR(10),CFT.BIRTH_DATE,121), '') BIRTH_DATE,
		ISNULL(DT.NEP_DATE, '') BIRTH_DATE_NEP,
	    ISNULL(CFTP.DOC_GUID, '') DOC_GUID,
	    ISNULL(CFTP.FILE_EXTENSION, '') FILE_EXTENSION,
	    ISNULL(CFTP.MIME_TYPE, '') MIME_TYPE,
		DS.DOC_FILE
    FROM CUSTOMER_FAMILY_TREE CFT
	    INNER JOIN RELATION R ON CFT.RELATION_CODE = R.RELATION_CODE
	    LEFT OUTER JOIN CUSTOMER_FAMILY_TREE_PHOTO CFTP ON CFT.TRAN_ID = CFTP.CUSTOMER_FAMILY_TREE_ID
		LEFT JOIN DOC_STORE DS ON DS.DOC_GUID=CFTP.DOC_GUID
		LEFT JOIN DATE_TABLE DT ON DT.ENG_DATE=CFT.BIRTH_DATE
    WHERE CUSTOMER_CODE = @CUSTOMER_CODE
	    AND CFT.[STATUS] = 1

	-- CUSTOMER GUARDIAN
	SELECT 
	    ISNULL(R.RELATION_NAME, '') RELATION_NAME,
	    ISNULL(CG.FULL_NAME, '') FULL_NAME,
		ISNULL(CONVERT(VARCHAR(10),CG.BIRTH_DATE,121), '') BIRTH_DATE,
		ISNULL(DT.NEP_DATE, '') BIRTH_DATE_NEP,
	    ISNULL(CGP.DOC_GUID, '') DOC_GUID,
	    ISNULL(CGP.FILE_EXTENSION, '') FILE_EXTENSION,
	    ISNULL(CGP.MIME_TYPE, '') MIME_TYPE,
		DS.DOC_FILE
    FROM CUSTOMER_GUARDIAN CG
	    INNER JOIN RELATION R ON CG.RELATION_CODE = R.RELATION_CODE
	    LEFT OUTER JOIN CUSTOMER_GUARDIAN_PHOTO CGP ON CG.TRAN_ID = CGP.CUSTOMER_GUARDIAN_ID
		LEFT JOIN DOC_STORE DS ON DS.DOC_GUID=CGP.DOC_GUID
		LEFT JOIN DATE_TABLE DT ON DT.ENG_DATE=CG.BIRTH_DATE
    WHERE CUSTOMER_CODE = @CUSTOMER_CODE
	    AND CG.[STATUS] = 1

     --CUSTOMER NOMINEE
	SELECT 
		ISNULL(R.RELATION_NAME, '') NOMINEE_RELATION_NAME,
	    ISNULL(CN.FULL_NAME, '') NOMINEE_FULL_NAME,
		--ISNULL(CN.[STATUS], '') [STATUS],
		ISNULL(CN.NOMINEE_CUSTOMER_CODE, '') NOMINEE_CUSTOMER_CODE,
		ISNULL(CONVERT(VARCHAR(10),CN.BIRTH_DATE,121), '') NOMINEE_BIRTH_DATE,
		ISNULL(DT.NEP_DATE, '') NOMINEE_BIRTH_DATE_NEP
	FROM CUSTOMER_NOMINEE CN
		INNER JOIN RELATION R ON CN.RELATION_CODE = R.RELATION_CODE
		LEFT JOIN DATE_TABLE DT ON DT.ENG_DATE=CN.BIRTH_DATE
	WHERE CUSTOMER_CODE = @CUSTOMER_CODE
		AND CN.[STATUS] = 1


	-- CUSTOMER BOARD OF DIRECTOR
	IF @IS_CUSTOMER_INDIVIDUAL = 0
	BEGIN
		SELECT 
			ISNULL(CBOD.FULL_NAME, '') FULL_NAME,
			ISNULL(CBOD.REPRESENTATIVE_INSTITUTION_NAME, '') REPRESENTATIVE_INSTITUTION_NAME,
			ISNULL(CBP.DOC_GUID, '') DOC_GUID,
			ISNULL(CBP.FILE_EXTENSION, '') FILE_EXTENSION,
			ISNULL(CBP.MIME_TYPE, '') MIME_TYPE
		FROM CUSTOMER_BOARD_OF_DIRECTOR CBOD
			LEFT OUTER JOIN CUSTOMER_BOD_PHOTO CBP ON CBOD.TRAN_ID = CBP.CUSTOMER_BOD_ID
		WHERE CBOD.CUSTOMER_CODE = @CUSTOMER_CODE
	END
END
--GO
--exec USP_CBS_Global_Get_CustomerInformation @CUSTOMER_CODE='001000018'