CREATE   PROC USP_CBS_TellerTransaction_Delete 
(  
	@TRAN_ID INT,
	@TRAN_USER_ID VARCHAR(50),
	@BR_CODE VARCHAR(50),
	@MSG varchar(500) = '' OUTPUT
)  
AS  
BEGIN 
	BEGIN TRY  
		DECLARE @MENU_CODE BIGINT =701
		DECLARE @TEMP_STATUS INT= ISNULL((SELECT TOP 1 [STATUS] FROM TELLER_TRAN_DETL WHERE TRAN_ID=@TRAN_ID),0)
		DECLARE @TRAN_DATE DATE=(SELECT TOP 1 DATE FROM DAYEND WHERE BR_CODE=@BR_CODE ORDER BY DATE DESC)
		SET @TRAN_USER_ID=(SELECT [USER_ID] FROM USERS WHERE LOGIN_NAME=@TRAN_USER_ID)

		IF NOT EXISTS(SELECT TOP 1 [STATUS] FROM TELLER_TRAN_DETL WHERE TRAN_ID=@TRAN_ID)
		BEGIN
			;THROW 50000, 'Warning - ! Transaction not found ...!!!', 1;
		END
		ELSE IF (@TEMP_STATUS = 1)
		BEGIN
			;THROW 50000, 'Warning - ! The transaction is already approved ...!!!', 1;
		END
		BEGIN TRANSACTION
			EXEC USP_DELETE_TRANSACTION_LOG 
				  @MENU_CODE = @MENU_CODE
				, @BR_CODE = @BR_CODE
				, @SOURCE_TABLE_NAME = 'TELLER_TRAN_DETL'
				, @SOURCE_TABLE_TRAN_ID =@TRAN_ID
				, @TRAN_USER_ID =@TRAN_USER_ID
				, @TRAN_DATE =@TRAN_DATE

			DECLARE @TELLER_AMOUNT  DECIMAL(18,2),@OPR_CODE varchar(2)

			SET @OPR_CODE = (SELECT OPR_CODE  FROM TELLER_TRAN_DETL WHERE TRAN_ID =@TRAN_ID)
			SET @TELLER_AMOUNT = ISNULL((SELECT ISNULL(SUM(ISNULL(MAST.TRAN_AMOUNT,0)),0)
									FROM TELLER_TRAN_DETL MAST
									WHERE MAST.TRAN_ID= @TRAN_ID
								),0)
			IF (@OPR_CODE IN ('06','07'))
			BEGIN
				EXEC USP_UPDATE_TELLER_TRANS @BR_CODE=@BR_CODE,@TRAN_DATE=@TRAN_DATE,@USER_ID=@TRAN_USER_ID,@USER_AS='T',@IN_OUT='IN',@CUR_CODE='01'
											,@AMOUNT=@TELLER_AMOUNT,@IN_OUT_USER_ID='',@IN_OUT_USER_AS='',@REVERTED='NO',@ACCEPTED='YES',@COUNTER='YES'
			END
			DELETE TELLER_TRAN_DETL_DENO WHERE TELLER_TRAN_DETL_ID=@TRAN_ID
			DELETE TELLER_TRAN_AML_INFO WHERE TELLER_TRAN_DETL_ID=@TRAN_ID
			DELETE TELLER_TRAN_DETL WHERE TRAN_ID = @TRAN_ID
			
		SET @MSG ='The transaction has been successfully deleted....!!!';
		COMMIT TRANSACTION 
	END TRY  
	BEGIN CATCH  
		IF @@TRANCOUNT > 0
			ROLLBACK TRANSACTION;
		;Throw;
	END CATCH  
END