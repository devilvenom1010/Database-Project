CREATE  PROCEDURE [dbo].[USP_CBS_TellerTransaction_Get_QueryList]
(
	@BR_CODE VARCHAR(50),
	@ROW_COUNT INT,
	@SKIP_COUNT INT,
	@STATUS INT,
	@VCH_NO VARCHAR(50)='',
	@AC_NO VARCHAR(50)='',
	@TOTAL_COUNT INT = 0 OUTPUT
)
AS
BEGIN
	SET NOCOUNT ON;
	DROP TABLE IF EXISTS #TEMP_TELLER
	DECLARE @OPR_DATE_TYPE CHAR(2) = (SELECT OPR_DATE_TYPE FROM GLOBAL_SETUP)

	 IF @STATUS NOT IN (1, 0, -2, 7, 2)
	  BEGIN  
		--RAISERROR('WARNING - ! Invalid Status...!!!', 18, 1);
		--RETURN; 
		EXEC USP_CBS_Global_Get_ErrorMessage @MSG_CODE = 'ERR_INVALID_STATUS'
      END 

	SELECT MM.*, CM.CUSTOMER_CODE, DBO.PROPERCASE(CM.FULL_NAME) CUSTOMER_NAME
	INTO #TEMP_TELLER 
	FROM (
		SELECT MAST.TRAN_ID, DAM.BR_CODE
			, MAST.OPR_CODE
			, DBO.PROPERCASE(OPR.OP_DESC) OP_DESC
			, DAM.AC_GROUP_CODE
			, DAM.AC_NO
			, ISNULL(MAST.VCH_NO, '') VCH_NO
			, MAST.TRAN_AMOUNT
			, MAST.[DESC1]
			, ISNULL(MAST.WDR_DEPOSITOR_NAME, '') WDR_DEPOSITOR_NAME
			, MAST.TRAN_USER_ID, USERS.LOGIN_NAME TRAN_USER_NAME, MAST.[STATUS]
			, CONVERT(VARCHAR(10),MAST.TRAN_DATE,121) TRAN_DATE
			, DT1.NEP_DATE TRAN_DATE_NEP
			, MAST.VALUE_DATE
			, DT2.NEP_DATE VALUE_DATE_NEP
			, REF_BR_CODE
			, BR.BR_NAME REF_BR_NAME
			, (CASE @OPR_DATE_TYPE WHEN 'BS' THEN Dt3.NEP_DATE ELSE CONVERT(VARCHAR(10), DT3.ENG_DATE) END) STATUS_CHANGE_DATE
			, MAST.STATUS_CHANGE_USER_ID
		FROM TELLER_TRAN_DETL MAST WITH(NOLOCK)
		INNER JOIN DEPOSIT_AC_MAST DAM WITH(NOLOCK) ON MAST.SOURCE_TYPE_ID = 3 AND MAST.SOURCE_TRAN_ID = DAM.ID 
		INNER JOIN USERS WITH(NOLOCK) ON USERS.[USER_ID] = MAST.TRAN_USER_ID
		INNER JOIN BRANCH BR WITH(NOLOCK) ON BR.BR_CODE = MAST.REF_BR_CODE
		INNER JOIN OPERATION_LIST OPR WITH(NOLOCK) ON OPR.OP_CODE = MAST.OPR_CODE
		INNER JOIN DATE_TABLE DT1 ON DT1.ENG_DATE = MAST.TRAN_DATE
		INNER JOIN DATE_TABLE DT2 ON DT2.ENG_DATE = MAST.VALUE_DATE
		INNER JOIN DATE_TABLE DT3 ON DT3.ENG_DATE = MAST.STATUS_CHANGE_DATE

		WHERE MAST.BR_CODE = @BR_CODE AND MAST.[STATUS] = @STATUS
		UNION ALL
		SELECT MAST.TRAN_ID, LM.BR_CODE
			, MAST.OPR_CODE
			, DBO.PROPERCASE(OPR.OP_DESC) OP_DESC
			, LM.AC_GROUP_CODE
			, LM.AC_NO
			, ISNULL(MAST.VCH_NO, '') VCH_NO
			, MAST.TRAN_AMOUNT
			, MAST.[DESC1]
			, ISNULL(MAST.WDR_DEPOSITOR_NAME, '') WDR_DEPOSITOR_NAME
			, MAST.TRAN_USER_ID, USERS.LOGIN_NAME TRAN_USER_NAME, MAST.[STATUS]
			, CONVERT(VARCHAR(10),MAST.TRAN_DATE,121) TRAN_DATE
			, CONVERT(VARCHAR(10), DT1.ENG_DATE)  TRAN_DATE_NEP
			, MAST.VALUE_DATE
			, CONVERT(VARCHAR(10), DT2.ENG_DATE)  VALUE_DATE_NEP
			, REF_BR_CODE
			, BR.BR_NAME REF_BR_NAME
			, (CASE @OPR_DATE_TYPE WHEN 'BS' THEN Dt3.NEP_DATE ELSE CONVERT(VARCHAR(10), DT3.ENG_DATE) END) STATUS_CHANGE_DATE
			, MAST.STATUS_CHANGE_USER_ID
		FROM TELLER_TRAN_DETL MAST WITH(NOLOCK)
		INNER JOIN LOAN_MAST LM WITH(NOLOCK) ON MAST.SOURCE_TYPE_ID = 4 AND MAST.SOURCE_TRAN_ID = LM.ID 
		INNER JOIN USERS WITH(NOLOCK) ON USERS.[USER_ID] = MAST.TRAN_USER_ID
		INNER JOIN BRANCH BR WITH(NOLOCK) ON BR.BR_CODE = MAST.REF_BR_CODE
		INNER JOIN OPERATION_LIST OPR WITH(NOLOCK) ON OPR.OP_CODE = MAST.OPR_CODE
		INNER JOIN DATE_TABLE DT1 ON DT1.ENG_DATE = MAST.TRAN_DATE
		INNER JOIN DATE_TABLE DT2 ON DT2.ENG_DATE = MAST.VALUE_DATE
		INNER JOIN DATE_TABLE DT3 ON DT3.ENG_DATE = MAST.STATUS_CHANGE_DATE
		WHERE MAST.BR_CODE = @BR_CODE AND MAST.[STATUS] = @STATUS
	) MM
	INNER JOIN (
				SELECT M.TRAN_ID,M.AC_NO,M.AC_GROUP_CODE,M.BR_CODE,[STATUS],ROW_NUMBER() OVER(PARTITION BY M.AC_NO ORDER BY M.TRAN_DATE DESC,M.TRAN_ID DESC) RN 
				FROM AC_HOLDER_MAST AS M 
				WHERE M.[STATUS]=1
			) AS AHHM ON AHHM.BR_CODE=MM.BR_CODE AND AHHM.AC_NO=MM.AC_NO AND AHHM.AC_GROUP_CODE=MM.AC_GROUP_CODE AND AHHM.RN=1
	INNER JOIN (
				SELECT AC_HOLDER_MAST_ID,TRAN_ID, CUSTOMER_CODE,ROW_NUMBER() OVER (PARTITION BY AC_HOLDER_MAST_ID ORDER BY IS_MASTER_AC_HOLDER DESC, TRAN_ID DESC)RN 
				FROM AC_HOLDER_DETL AS D
			) AS AHHD ON AHHM.TRAN_ID=AHHD.AC_HOLDER_MAST_ID AND AHHD.RN=1
	INNER JOIN CUSTOMER_MAST CM ON CM.CUSTOMER_CODE = AHHD.CUSTOMER_CODE

		SELECT MAST.TRAN_ID,
               MAST.BR_CODE,
               MAST.OPR_CODE,
               MAST.OP_DESC,
               MAST.AC_GROUP_CODE,
               MAST.AC_NO,
               MAST.CUSTOMER_CODE,
               MAST.CUSTOMER_NAME,
               MAST.VCH_NO,
               MAST.TRAN_AMOUNT,
               MAST.DESC1,
               MAST.WDR_DEPOSITOR_NAME,
               MAST.TRAN_USER_ID,
               MAST.TRAN_USER_NAME,
               MAST.[STATUS],
               MAST.TRAN_DATE,
			   MAST.TRAN_DATE_NEP
		FROM #TEMP_TELLER MAST 
		WHERE MAST.VCH_NO LIKE ISNULL(NULLIF(ISNULL(@VCH_NO,''),''),VCH_NO) + '%'
			AND MAST.AC_NO LIKE ISNULL(NULLIF(ISNULL(@AC_NO,''),''),AC_NO) + '%'
		ORDER BY MAST.TRAN_ID DESC
		OFFSET @SKIP_COUNT ROWS
		FETCH NEXT @ROW_COUNT ROWS ONLY

		SET @TOTAL_COUNT =  (SELECT COUNT(1) FROM #TEMP_TELLER MAST
							WHERE MAST.VCH_NO LIKE ISNULL(NULLIF(ISNULL(@VCH_NO,''),''),VCH_NO) + '%'
							AND MAST.AC_NO LIKE ISNULL(NULLIF(ISNULL(@AC_NO,''),''),AC_NO) + '%')

	SELECT @TOTAL_COUNT TOTAL_COUNT
	DROP TABLE IF EXISTS #TEMP_TELLER
END
--GO
--EXEC [dbo].[USP_CBS_TellerTransaction_Get_QueryList]
--	@BR_CODE ='001',
--	@ROW_COUNT =10,
--	@SKIP_COUNT =0,
--	@STATUS =-2