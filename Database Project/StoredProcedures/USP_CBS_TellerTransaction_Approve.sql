CREATE  PROCEDURE USP_CBS_TellerTransaction_Approve
(
	@BR_CODE VARCHAR(50),
	@APPROVAL_USER_ID VARCHAR(50),
	@TRAN_ID INT,
	@APPROVAL_ACTION INT = 3,
	@REMARKS VARCHAR(256),
	@VCH_NO VARCHAR(50) = NULL OUTPUT,
	@MSG VARCHAR(500) = NULL OUTPUT
)
AS
BEGIN
	BEGIN TRY
	DECLARE @SCREEN_CODE VARCHAR(10) = '02101', @MENU_CODE INT = 701;
	DECLARE @IS_FINAL_APPROVAL BIT = 0
		, @MAX_APPROVAL_LEVEL INT = 0
		, @REMAINING_APPROVAL_COUNT INT = 0
		, @APPROVAL_DATE DATE
	DROP TABLE IF EXISTS #VCH
	SET @APPROVAL_USER_ID=(SELECT TOP 1 [USER_ID] FROM USERS WHERE LOGIN_NAME=@APPROVAL_USER_ID)
	SET @APPROVAL_DATE =(SELECT TOP 1 DATE FROM DAYEND WHERE BR_CODE=@BR_CODE ORDER BY DATE DESC)
	/* === VALIDATIONS: START === */
	IF @APPROVAL_ACTION NOT IN (1,2,3)
		THROW 50000, 'Warning ! - Invalid Approval Action...!!!', 1;

	IF NOT EXISTS (SELECT TOP 1 TRAN_ID FROM TELLER_TRAN_DETL WHERE TRAN_ID=@TRAN_ID)
		THROW 50000, 'Warning ! - Invalid Transaction Id...!!!', 1;
	--IF EXISTS (SELECT TOP 1 1 FROM USERS WHERE [USER_ID] = @APPROVAL_USER_ID AND [ADMIN] = 'YES')
	--	THROW 50000, 'Warning ! - Admin are not allowed to carry out transactions...!!!', 1;

	DECLARE @CURRENT_STATUS INT = (SELECT TOP 1 [STATUS] FROM TELLER_TRAN_DETL WHERE TRAN_ID = @TRAN_ID);
	IF @CURRENT_STATUS = 2
		THROW 50000, 'Warning ! - The specified transaction has already been sent for review...!!!', 1;
	IF @CURRENT_STATUS = 3
		THROW 50000, 'Warning ! - The specified transaction has already been rejected...!!!', 1;
	IF @CURRENT_STATUS = 1
		THROW 50000, 'Warning ! - The specified transaction has already been approved...!!!', 1;
	IF @CURRENT_STATUS NOT IN (0,7)
		THROW 50000, 'Warning ! - The specified transaction has invalid status...!!!', 1;

	DECLARE @REF_BR_CODE VarChar(50)
		, @ENTRY_BR_CODE VarChar(50)
		, @OPR_CODE VarChar(2)
		, @CENTER_CODE VarChar(50)
		, @GROUP_CODE VarChar(50)
		, @CUSTOMER_CODE VarChar(50)
		, @COA_TRAN_ID VarChar(50)
		, @AC_GROUP_CODE VarChar(50)
		, @GL_CODE VarChar(50)
		, @BANK_CODE VarChar(50)
		, @AC_NO VarChar(50)
		, @TRAN_AMOUNT Decimal(18,2)
		, @DESC1 VarChar(200)
		, @INST_DATE Date
		, @INST_CODE VarChar(50)
		, @INST_NO VarChar(50)
		, @SOURCE_OF_FUND_ID Int
		, @WDR_DEPOSITOR_NAME VarChar(100)
		, @TRAN_USER_ID VarChar(50)
		, @TRAN_DATE Date
		, @BANK_BR_CODE varchar(50)
		, @REF_AC_NO VarChar(50)
		, @SOURCE_BANK_BRCODE VarChar(50)
		, @SOURCE_AC_NO  VARCHAR(50)
		, @SOURCE_CHQ_NO VARCHAR(50)
		, @SOURCE_CHQ_DATE DATE	

	DECLARE @IS_VALID BIT,@ERROR_MSG VARCHAR(MAX);

	SELECT @REF_BR_CODE = MAST.REF_BR_CODE
		, @ENTRY_BR_CODE = MAST.BR_CODE
		, @OPR_CODE = MAST.OPR_CODE
		, @CENTER_CODE = MAST.CENTER_CODE
		, @GROUP_CODE = MAST.GROUP_CODE
		, @CUSTOMER_CODE = MAST.CUSTOMER_CODE
		, @COA_TRAN_ID = MAST.COA_TRAN_ID
		, @AC_GROUP_CODE = MAST.AC_GROUP_CODE
		, @GL_CODE = MAST.GL_CODE
		, @BANK_CODE = MAST.BANK_CODE
		, @AC_NO = MAST.AC_NO
		, @TRAN_AMOUNT = MAST.TRAN_AMOUNT
		, @DESC1 = MAST.DESC1
		, @INST_DATE =CASE WHEN MAST.OPR_CODE='14' OR MAST.OPR_CODE ='15' THEN MAST.SOURCE_CHQ_DATE ELSE MAST.INST_DATE END -- 
		, @INST_CODE = MAST.INST_CODE
		, @INST_NO =CASE WHEN MAST.OPR_CODE='14' OR MAST.OPR_CODE ='15' THEN MAST.SOURCE_CHQ_NO ELSE MAST.INST_NO END 
		, @SOURCE_OF_FUND_ID = MAST.SOURCE_OF_FUND_ID
		, @WDR_DEPOSITOR_NAME = MAST.WDR_DEPOSITOR_NAME
		, @TRAN_USER_ID = MAST.TRAN_USER_ID
		, @TRAN_DATE = @APPROVAL_DATE--MAST.TRAN_DATE
		, @REF_AC_NO = MAST.REF_AC_NO
		, @BANK_BR_CODE=CASE WHEN MAST.OPR_CODE<>'13' THEN MAST.BR_CODE ELSE BA.BR_CODE END 
		, @SOURCE_BANK_BRCODE=ISNULL(DAM.BR_CODE,'')
		, @SOURCE_AC_NO=MAST.SOURCE_AC_NO
		, @SOURCE_CHQ_NO=MAST.SOURCE_CHQ_NO
		, @SOURCE_CHQ_DATE=MAST.SOURCE_CHQ_DATE
	FROM TELLER_TRAN_DETL MAST
		LEFT OUTER JOIN BANK ON BANK.BANK_CODE = MAST.BANK_CODE
		LEFT OUTER JOIN BANK_ACNO BA ON BANK.BANK_CODE = BA.BANK_CODE AND BA.NOSTRO_AC_NO=MAST.REF_AC_NO AND BA.APPROVED='YES' AND AC_CLOSE<>'YES'
		LEFT OUTER JOIN DEPOSIT_AC_MAST DAM ON DAM.AC_NO= MAST.SOURCE_AC_NO
	WHERE MAST.TRAN_ID = @TRAN_ID
		AND MAST.[STATUS] IN (0, 7) -- NEW AND UNDER APPROVAL
		AND ISNULL([VCH_NO],'') =''

	-------------------------------------limit validation -----------------------------------------------
	DECLARE @DR_CR VARCHAR(2);
	SET @DR_CR=(SELECT TOP 1 DR_CR FROM OPERATION_LIST WHERE OP_CODE=@OPR_CODE)
	DECLARE @DR_AMT DECIMAL(18,2) , @CR_AMT DECIMAL(18,2),@AGAINST_AC VARCHAR(50) = CASE WHEN @REF_AC_NO IS NULL OR @REF_AC_NO = '' THEN @SOURCE_AC_NO ELSE   @REF_AC_NO END 		
	SELECT @DR_AMT= CASE WHEN @DR_CR = 'DR' THEN @TRAN_AMOUNT ELSE 0 END, @CR_AMT= CASE WHEN @DR_CR = 'CR' THEN @TRAN_AMOUNT ELSE 0 END	
	
	EXEC USP_TELLER_TRANSACTION_DEPOSIT_WITHDRAW_LIMIT_VALIDATION @AC_NO =@AC_NO,@TRAN_DATE =@TRAN_DATE,@DR_TRAN_AMT=@DR_AMT,@CR_TRAN_AMT=@CR_AMT
	
	IF @OPR_CODE = '12' OR @OPR_CODE='14' OR @OPR_CODE='15'
	BEGIN 	
	SELECT @CR_AMT= CASE WHEN @DR_CR = 'DR' THEN @TRAN_AMOUNT ELSE 0 END, @DR_AMT= CASE WHEN @DR_CR = 'CR' THEN @TRAN_AMOUNT ELSE 0 END	
	EXEC USP_TELLER_TRANSACTION_DEPOSIT_WITHDRAW_LIMIT_VALIDATION @AC_NO =@AGAINST_AC,@TRAN_DATE =@TRAN_DATE,@DR_TRAN_AMT=@DR_AMT,@CR_TRAN_AMT=@CR_AMT
	END 
   ----------------------------------------------------------------------------------------------------------
	
	--
	IF(SELECT TOP 1 DR_CR FROM OPERATION_LIST WHERE OP_CODE = @OPR_CODE) = 'DR'
	BEGIN
		EXEC USP_CHECKDEPOSITWDR @AC_NO=@AC_NO,@CUR_CODE='01',@CUR_WDR_AMT=@TRAN_AMOUNT,@BR_CODE=@REF_BR_CODE,@EFFECT_DATE=@TRAN_DATE,@IS_VALID=@IS_VALID OUTPUT,@ERROR_MSG=@ERROR_MSG OUTPUT
		IF @IS_VALID=0 
		THROW 50000, @ERROR_MSG, 1;
	END
	--

	DECLARE @CHARGE_LIST AS TYPE_CBS_TellerTransaction_Charge,
			@DENO AS TYPE_CBS_TellerTransaction_Deno;

	INSERT INTO @CHARGE_LIST(Charge_Amt, Charge_Code)
	SELECT CHARGE_AMT , CHARGE_CODE
	FROM TELLER_TRAN_DETL_CHARGE
	WHERE TELLER_TRAN_DETL_ID =@TRAN_ID

	INSERT INTO @DENO(DENO, DENO_CNT)
	SELECT DENO_COUNT, DENO_VALUE
	FROM TELLER_TRAN_DETL_DENO
	WHERE TELLER_TRAN_DETL_ID = @TRAN_ID
	

	CREATE TABLE #VCH (GL_CODE VARCHAR(50),AC_NO VARCHAR(50),DESC1 VARCHAR(8000),AMT DECIMAL(18,2),BR_CODE VARCHAR(50),INST_CODE VARCHAR(50),INST_NO VARCHAR(50),INST_DATE DATE,VCH_TYPE INT)
	DECLARE @IS_ABBS BIT = CASE WHEN @ENTRY_BR_CODE <> @REF_BR_CODE THEN 1 ELSE 0 END,@NEW_BAL DECIMAL(18,2);
	DECLARE @VCH AS TYPE_CBS_Voucher;
	DECLARE @BANK_GL_CODE VARCHAR(50) = (SELECT TOP 1 BANK_GL_CODE FROM AC_CHART WHERE GL_CODE=@GL_CODE )

	DECLARE @AC_NAME VARCHAR(1000) = (SELECT TOP 1 AC_NAME FROM 
										(SELECT AC_NAME FROM DEPOSIT_AC_MAST WHERE AC_NO =@AC_NO
										UNION 
										SELECT AC_NAME FROM LOAN_MAST WHERE AC_NO =@AC_NO
										)T
									)

	EXEC USP_TELLER_TRAN_GENERATE_VOUCHER @TRAN_ID, 1, 0, @CENTER_CODE, @GROUP_CODE, @CUSTOMER_CODE,
			@ENTRY_BR_CODE, @AC_NO,@AC_NAME, @OPR_CODE, @TRAN_DATE, @TRAN_USER_ID, @TRAN_AMOUNT, @BANK_CODE,
			@REF_AC_NO, @BANK_GL_CODE, @REF_AC_NO, @INST_NO, @INST_CODE, @INST_DATE, @DENO, @CHARGE_LIST,
			@DESC1, @SOURCE_OF_FUND_ID, @WDR_DEPOSITOR_NAME, @IS_ABBS, @REF_BR_CODE,@BANK_BR_CODE,
			@SOURCE_BANK_BRCODE  ,@SOURCE_AC_NO,@SOURCE_CHQ_NO,@SOURCE_CHQ_DATE,
			@APPROVAL_USER_ID,
			@BR_CODE, NULL, @NEW_BAL OUTPUT, NULL, @MAX_APPROVAL_LEVEL OUTPUT

	INSERT INTO @VCH(GL_CODE,AC_NO,DESC1,AMT,BR_CODE,INST_CODE,INST_NO,INST_DATE,VCH_TYPE)
	SELECT GL_CODE,AC_NO,DESC1,AMT,BR_CODE,INST_CODE,INST_NO,INST_DATE,VCH_TYPE
	FROM #VCH

	--LENGTH VALIDATION FOR DESCRIPTION
	DECLARE @DESC_LEN_COUNT INT = (SELECT MAX(LEN(DESC1)) from @VCH);
	if @DESC_LEN_COUNT > 200
		THROW 50000, 'Warning - ! Description length exceeded, must be less or equal to 200...!!!', 1;

	/* === VALIDATIONS: END === */

	BEGIN TRANSACTION
		EXEC USP_TRANSACTION_LOG_APPROVAL_ACTION
			@MENU_CODE = @MENU_CODE,
			@BR_CODE = @BR_CODE,
			@SOURCE_TABLE_TRAN_ID = @TRAN_ID,
			@TRAN_USER_ID = @APPROVAL_USER_ID,
			@TRAN_DATE = @APPROVAL_DATE,
			@ACTION_OPTION = @APPROVAL_ACTION, --1. Review, 2.Reject, 3.Approved
			@ACTION_REMARKS = @REMARKS,
			@MAX_APPROVAL_LEVEL = @MAX_APPROVAL_LEVEL,
			@REMAINING_APPROVAL_COUNT = @REMAINING_APPROVAL_COUNT OUTPUT,
			@IS_FINAL_APPROVAL = @IS_FINAL_APPROVAL OUTPUT

		IF @APPROVAL_ACTION = 3 AND @IS_FINAL_APPROVAL = 0
		BEGIN
			UPDATE MAST
			SET [STATUS] = 7,
				STATUS_CHANGE_DATE = @APPROVAL_DATE,
				STATUS_CHANGE_USER_ID = @APPROVAL_USER_ID,
                --- ADDED
				VALUE_DATE=@APPROVAL_DATE
				FROM TELLER_TRAN_DETL MAST
			WHERE MAST.TRAN_ID = @TRAN_ID
				AND [STATUS] IN (0, 7) -- NEW AND UNDER APPROVAL
				AND ISNULL([VCH_NO],'') =''

			SET @MSG = CONCAT('The transaction has been approved and sent for further higher approval ! (Remaining Approval Count: ', @REMAINING_APPROVAL_COUNT , ' with Transaction Id: ',@TRAN_ID,' ).');
		END
		ELSE
		IF @APPROVAL_ACTION = 3 AND @IS_FINAL_APPROVAL = 1
		BEGIN
		
			EXEC USP_TELLER_TRAN_SAVE_VOUCHER @TRAN_ID, 1, 0, @ENTRY_BR_CODE, @AC_NO, @OPR_CODE, @TRAN_DATE
                                            , @TRAN_USER_ID, @TRAN_AMOUNT, @REF_AC_NO, @INST_NO, @INST_CODE
                                            , @INST_DATE, @DENO, @CHARGE_LIST, @DESC1, @SOURCE_OF_FUND_ID
                                            , @WDR_DEPOSITOR_NAME, @IS_ABBS, @REF_BR_CODE,@BANK_BR_CODE
                                            , @SOURCE_BANK_BRCODE,@SOURCE_AC_NO,@SOURCE_CHQ_NO,@SOURCE_CHQ_DATE
                                            , @APPROVAL_USER_ID, @BR_CODE, @VCH_NO OUTPUT, @VCH
			
			UPDATE MAST
			SET [STATUS] = 1,
				STATUS_CHANGE_DATE = @APPROVAL_DATE,
				STATUS_CHANGE_USER_ID = @APPROVAL_USER_ID,
				VCH_NO = @VCH_NO,
                -- ADDED
				VALUE_DATE=@APPROVAL_DATE
			FROM TELLER_TRAN_DETL MAST
			WHERE MAST.TRAN_ID = @TRAN_ID
				AND [STATUS] IN (0, 7) -- NEW AND UNDER APPROVAL
				AND ISNULL([VCH_NO],'') =''

			UPDATE MAST
			SET [STATUS] = 1,
				STATUS_CHANGE_DATE = @APPROVAL_DATE,
				STATUS_CHANGE_USER_ID = @APPROVAL_USER_ID
			FROM TRANSACTION_APPROVAL_FLOW_LOG_MAST MAST
			WHERE MAST.SOURCE_TABLE_TRAN_ID = @TRAN_ID
				AND [STATUS] IN (0, 7) -- NEW AND UNDER APPROVAL
				AND MAST.MENU_CODE = @MENU_CODE
			DECLARE @TOT_CHARGE_AMT DECIMAL(18,2) = ISNULL((SELECT SUM(ISNULL(CHARGE_AMT,0)) FROM @CHARGE_LIST),0)

			IF @IS_FINAL_APPROVAL=1 AND @TOT_CHARGE_AMT>0 AND @OPR_CODE='01' -- APPROVE CASE
			--IF @IS_FINAL_APPROVAL=1 AND @TOT_CHARGE_AMT>0 AND @OPR_CODE<>'12' -- APPROVE CASE
			BEGIN
				EXEC USP_UPDATE_TELLER_TRANS @BR_CODE=@BR_CODE,@TRAN_DATE=@TRAN_DATE,@USER_ID=@TRAN_USER_ID,@USER_AS='T',@IN_OUT='IN',@CUR_CODE='01',@AMOUNT=@TOT_CHARGE_AMT,@IN_OUT_USER_ID='',@IN_OUT_USER_AS='',@REVERTED='NO',@ACCEPTED='YES',@COUNTER='YES'
			END
			IF EXISTS(SELECT TOP  1 1 FROM TELLER_TRAN_DETL  TTD 
						INNER JOIN TELLER_TRAN_AML_INFO TTAI ON TTAI.TELLER_TRAN_DETL_ID = TTD.TRAN_ID
						WHERE TTD.TRAN_ID=@TRAN_ID)
			BEGIN
				INSERT INTO GLTRAN_AML_INFO(VCH_NO,AML_TRAN_CODE, DRAWEE_FIRST_NAME,DRAWEE_MIDDLE_NAME,DRAWEE_LAST_NAME,DRAWEE_CUSTOMER_CODE,CONDUCTOR_FIRST_NAME,CONDUCTOR_MIDDLE_NAME,CONDUCTOR_LAST_NAME,CONDUCTOR_CUSTOMER_CODE,CONDUCTOR_IDENTITY_TYPE_ID,CONDUCTOR_IDENTITY_NO,CONDUCTOR_IDENTITY_ISSUE_DATE,CONDUCTOR_IDENTITY_EXPIRY_DATE,CONDUCTOR_IDENTITY_ISSUE_PLACE,CONDUCTOR_BIRTH_DATE,CONDUCTOR_CONTACT_NO,CONDUCTOR_DISTRICT_CODE,CONDUCTOR_FATHER_NAME,CONDUCTOR_GENDER_CODE,CONDUCTOR_GRAND_FATHER_NAME,CONDUCTOR_LOCAL_BODY_CODE,CONDUCTOR_LOCAL_BODY_TYPE_ID,CONDUCTOR_OCCUPATION_CODE,CONDUCTOR_TOLE_NAME)
				SELECT TOP 1 @VCH_NO,AML_TRAN_CODE, DRAWEE_FIRST_NAME, DRAWEE_MIDDLE_NAME,DRAWEE_LAST_NAME, DRAWEE_CUSTOMER_CODE,CONDUCTOR_FIRST_NAME, CONDUCTOR_MIDDLE_NAME,CONDUCTOR_LAST_NAME,CONDUCTOR_CUSTOMER_CODE,CONDUCTOR_IDENTITY_TYPE_ID, CONDUCTOR_IDENTITY_NO, CONDUCTOR_IDENTITY_ISSUE_DATE, CONDUCTOR_IDENTITY_EXPIRY_DATE, CONDUCTOR_IDENTITY_ISSUE_PLACE,CONDUCTOR_BIRTH_DATE,CONDUCTOR_CONTACT_NO,CONDUCTOR_DISTRICT_CODE,CONDUCTOR_FATHER_NAME,CONDUCTOR_GENDER_CODE,CONDUCTOR_GRAND_FATHER_NAME,CONDUCTOR_LOCAL_BODY_CODE,CONDUCTOR_LOCAL_BODY_TYPE_ID,CONDUCTOR_OCCUPATION_CODE,CONDUCTOR_TOLE_NAME
				FROM TELLER_TRAN_AML_INFO WHERE TELLER_TRAN_DETL_ID=@TRAN_ID
			END

			SET @MSG = formatmessage('The transaction has been approved successfully ! (Voucher No.: %s)', @VCH_NO);
		END
		SELECT @TRAN_ID TRAN_ID, ISNULL(@VCH_NO, '') VCH_NO, @MSG MSG, @IS_FINAL_APPROVAL IS_FINAL_APPROVAL, @REMAINING_APPROVAL_COUNT REMAINING_APPROVAL_COUNT, @MAX_APPROVAL_LEVEL MAX_APPROVAL_LEVEL
	DROP TABLE IF EXISTS #VCH
	COMMIT TRANSACTION
	END TRY
	BEGIN CATCH
	 IF @@TRANCOUNT > 0
		ROLLBACK TRAN;
	;THROW;
	END CATCH
END