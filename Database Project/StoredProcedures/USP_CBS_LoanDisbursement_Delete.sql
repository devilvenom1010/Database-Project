CREATE  PROC USP_CBS_LoanDisbursement_Delete
(
	@TRAN_ID INT,
	@BR_CODE VARCHAR(50),
	@TRAN_USER_ID VARCHAR(50),
	@MSG varchar(500) = '' OUTPUT
)
AS
BEGIN
	BEGIN TRY
		DECLARE @MENU_CODE BIGINT=803, @SCREEN_CODE VARCHAR(20)= (SELECT TOP 1 SCREEN_CODE FROM APP_MENU WHERE MENU_CODE=803)
		SET @TRAN_USER_ID=(SELECT [USER_ID] FROM USERS WHERE LOGIN_NAME=@TRAN_USER_ID)

		DECLARE @TRAN_DATE DATE=(SELECT TOP 1 DATE FROM DAYEND WHERE BR_CODE=@BR_CODE ORDER BY DATE DESC)

		IF NOT EXISTS (SELECT TOP 1 'X' FROM LOAN_DISBURSE_TRAN WHERE TRAN_ID=@TRAN_ID)
		BEGIN
			;THROW 50000, 'Warning - ! Transaction not found ...!!!', 1;
		END

		DECLARE @TEMP_STATUS INT= ISNULL((SELECT TOP 1 [STATUS] FROM LOAN_DISBURSE_TRAN WHERE TRAN_ID=@TRAN_ID),0)
		IF (@TEMP_STATUS = 1)
		BEGIN
			;THROW 50000, 'Warning - ! The transaction is already approved ...!!!', 1;
		END

		IF ISNULL((SELECT COUNT(BR_CODE) FROM DAYEND WHERE (BR_CODE=@BR_CODE) AND DATE=@TRAN_DATE AND DAYEND.ACTION_OPT IN ('C','P')),0)<>1
		BEGIN
			;THROW 50000, 'Warning - ! Invalid Transaction Date ...!!!', 1;
		END
		
		IF DBO.USERHASMENURIGHTS(@TRAN_USER_ID,@TRAN_DATE,@BR_CODE,@SCREEN_CODE,3)=0      
		BEGIN      
			;THROW 50000, 'Warning - ! Insufficient Privileges. Please Check Menu Rights For DELETE Option...!!!', 1;
		END 
		
		DECLARE @CURRENT_STATUS INT, @TRAN_TYPE INT, @TXN_LIST AS TYPE_CBS_GlTransaction, @TRANSACTION_APPROVAL_FLOW_LOG_MAST_ID INT
		BEGIN TRANSACTION	
					
			SELECT @CURRENT_STATUS=[STATUS], @TRAN_TYPE=PAYMENT_MODE FROM LOAN_DISBURSE_TRAN WHERE TRAN_ID=@TRAN_ID

			SET @TRANSACTION_APPROVAL_FLOW_LOG_MAST_ID=(SELECT TOP 1 TRAN_ID FROM TRANSACTION_APPROVAL_FLOW_LOG_MAST 
			WHERE MENU_CODE = @MENU_CODE AND SOURCE_TABLE_TRAN_ID = @TRAN_ID)

			INSERT INTO @TXN_LIST (GL_CODE, AMOUNT)
			SELECT GL_CODE, AMOUNT AMOUNT
			FROM TRANSACTION_APPROVAL_FLOW_GL_DETL
			WHERE TRANSACTION_APPROVAL_FLOW_LOG_MAST_ID = @TRANSACTION_APPROVAL_FLOW_LOG_MAST_ID

			DECLARE @TELLER_AMOUNT DECIMAL(18,2) = (
				SELECT TOP 1 TXN.AMOUNT
				FROM @TXN_LIST TXN
					INNER JOIN CURRENCY CUR ON TXN.GL_CODE = CUR.TELLER_GL_CODE AND CUR.CUR_CODE='01'
			);

			IF @TELLER_AMOUNT <> 0 AND @CURRENT_STATUS=0
			BEGIN
				EXEC USP_UPDATE_TELLER_TRANS @BR_CODE=@BR_CODE,@TRAN_DATE=@TRAN_DATE,@USER_ID=@TRAN_USER_ID,@USER_AS='T',@IN_OUT='IN',@CUR_CODE='01',@AMOUNT=@TELLER_AMOUNT,@IN_OUT_USER_ID='',@IN_OUT_USER_AS='',@REVERTED='NO',@ACCEPTED='YES',@COUNTER='YES'
			END
			--------------------------------------------------------
			DELETE FROM LOAN_DISBURSE_SECONDARY_INSURANCE_POLICY WHERE LOAN_DISBURSE_TRAN_ID=@TRAN_ID
			DELETE FROM LOAN_DISBURSE_PRIMARY_INSURANCE_POLICY WHERE LOAN_DISBURSE_TRAN_ID=@TRAN_ID
			DELETE FROM LOAN_DISBURSE_SCHEDULE WHERE LOAN_DISBURSE_TRAN_ID=@TRAN_ID
			DELETE FROM LOAN_DISBURSE_SCHEDULE_INFO WHERE LOAN_DISBURSE_TRAN_ID=@TRAN_ID
			DELETE FROM LOAN_DISBURSE_EXTRA_AC WHERE LOAN_DISBURSE_TRAN_ID=@TRAN_ID
			DELETE FROM LOAN_DISBURSE_INTEREST_SCHEDULE WHERE LOAN_DISBURSE_TRAN_ID=@TRAN_ID
			DELETE FROM LOAN_DISBURSE_CHARGE WHERE LOAN_DISBURSE_TRAN_ID=@TRAN_ID
			DELETE FROM LOAN_DISBURSE_TRAN WHERE TRAN_ID=@TRAN_ID

			 UPDATE TRANSACTION_APPROVAL_FLOW_LOG_MAST 			
			 SET [STATUS]=2, STATUS_CHANGE_DATE= @TRAN_DATE, STATUS_CHANGE_USER_ID= @TRAN_USER_ID 			
			 WHERE SOURCE_TABLE_TRAN_ID = @TRAN_ID AND MENU_CODE = @MENU_CODE

			SET @MSG = 'The transaction has been successfully deleted....!!!';

		COMMIT TRANSACTION
	END TRY
	BEGIN CATCH
		IF @@TRANCOUNT > 0    
			ROLLBACK TRANSACTION;    
		;THROW;
		RETURN;
	END CATCH
END