CREATE  PROCEDURE USP_CBS_LoanDisbursement_Get_LoanAccountDetails
(
	@AC_NO VARCHAR(50),
	@BR_CODE VARCHAR(5)
)
AS
BEGIN
	SET NOCOUNT ON;
	SET ARITHABORT ON;

	DECLARE @TRAN_DATE DATE=(SELECT TOP 1 DATE FROM DAYEND WHERE BR_CODE=@BR_CODE ORDER BY DATE DESC)
	DECLARE @AC_GROUP_CODE VARCHAR(50)=(SELECT TOP 1 AC_GROUP_CODE FROM LOAN_MAST WHERE AC_NO=@AC_NO)
	DROP TABLE IF EXISTS #LOAN

	SELECT TOP 1 
		LM.AC_NO , AC_NAME , LM.AC_GROUP_CODE, AG.AC_GROUP_NAME, LM.INT_SCHEME_TYPE
	    , AGP.ALLOW_MORATORIUM , AGP.MAX_MP_DAYS, F.FREQ_NAME , AG.AC_GROUP_TYPE_CODE
	    ,CONVERT(VARCHAR(10),LOAN_OPEN_DATE,121) LOAN_OPEN_DATE
		, CONVERT(VARCHAR(10),LOAN_MATURE_DATE,121) LOAN_MATURE_DATE
		, AGAINST_DEPOSIT_AC_NO
	    , LM.[PERIOD], LM.PERIOD_UNIT_CODE, INT_RATE, IIF(SCH_TYPE='1','EP','EI') SCH_TYPE, LM.SCHEDULE
	    , CAST(CASE WHEN AGP.INSURED_LOAN = 'YES' THEN 1 ELSE 0 END AS BIT) IS_INSURED
		--INSURANCE
	    , ISNULL(PREM.APPLY_MODE, 0) AS INS_PREMIUM_RATE
		, CAST(ISNULL(PREM.IS_PREMIUM_PER_ANNUM, 1) AS BIT) AS PERIOD_BASED_INSURANCE
		, ISNULL(PREM.UPTO_MONTH, 0) AS UPTO_INSURANCE_MONTH
		, AGP.INSURANCE_PCT, INSURED_TYPE
	    , INST_TYPE_CODE, IPFREQ_FOR_GROUP_LOAN, SCH_VS_NO 
		, SCH_START_DATE = CASE WHEN (LM.SCHEDULE = 'YES' AND LM.SCH_VS_NO = 0 AND LOAN_OPEN_DATE = @TRAN_DATE) THEN CONVERT(VARCHAR(10),LOAN_OPEN_DATE,121)  ELSE CONVERT(VARCHAR(10),SCH_START_DATE,121) END
	    , ISNULL(AGP.SCH_SP_DAY, 0) SCH_SP_DAY
	    , PPFREQ_FOR_GROUP_LOAN , ISNULL(SKIP_MEET_CNT_IN_GROUP_LOAN, 0) SKIP_MEET_CNT_IN_GROUP_LOAN
	    , ISNULL(EI_INST_AMT, 0) EI_INST_AMT , ISNULL(INST_PRN_AMT, 0) INST_PRN_AMT 
		, ISNULL(LM.MP_DAYS, 0) MP_DAYS
	    , ISNULL(AMT.BAL, 0) BAL , ISNULL(AC.LIMIT, 0) LIMIT , ISNULL((CASE WHEN AG.AC_TYPE_SUB_TYPE_CODE = '0401' THEN ISNULL(AMT.BAL, 0) ELSE AC.USED_LIMIT END), 0) USED_LIMIT
	    , ISNULL((AC.LIMIT - (CASE WHEN AG.AC_TYPE_SUB_TYPE_CODE = '0401' THEN ISNULL(AMT.BAL, 0) ELSE AC.USED_LIMIT END) ), 0) OS_LIMIT, ISNULL(AMT.RI, 0) AS RI_BAL
		, PERIOD_IN_MONTH = DBO.GetMonthDifference(LM.LOAN_OPEN_DATE, LM.LOAN_MATURE_DATE,GS.OPR_DATE_TYPE, 1) --CALCULATION OF INSURANCE MONTHS
		, IS_INITIAL_DISBURSE = CAST(CASE WHEN (LM.SCHEDULE = 'YES' AND LM.SCH_VS_NO = 0 AND LOAN_OPEN_DATE = @TRAN_DATE) THEN 1 ELSE 0 END AS BIT)
	INTO #LOAN
	FROM LOAN_MAST LM WITH(NOLOCK)
	INNER JOIN GLOBAL_SETUP GS ON 1 = 1
	INNER JOIN AC_GROUP AG WITH(NOLOCK) ON LM.AC_GROUP_CODE = AG.AC_GROUP_CODE AND AG.AC_TYPE_CODE = '04' 
	INNER JOIN AC_GROUP_PARA AGP WITH(NOLOCK) ON AGP.AC_GROUP_CODE = LM.AC_GROUP_CODE
	LEFT OUTER JOIN FREQUENCY F ON LM.INT_COL_FREQ_CODE = F.FREQ_CODE
	LEFT OUTER JOIN (SELECT SUM(AL.ASSIGNED_LIMIT) AS LIMIT,ISNULL(SUM(ALUD.TRAN_AMOUNT),0) USED_LIMIT,BR_CODE,AC_NO 
					FROM AC_LIMIT AL WITH(NOLOCK)
						LEFT OUTER JOIN (SELECT SUM(ALUD.TRAN_AMOUNT) TRAN_AMOUNT,ALUD.AC_LIMIT_ID FROM AC_LIMIT_USED_DETL ALUD WITH(NOLOCK) GROUP BY ALUD.AC_LIMIT_ID) ALUD ON ALUD.AC_LIMIT_ID=AL.TRAN_ID
					WHERE [STATUS_CHANGE_DATE]<=@TRAN_DATE AND STATUS=1
					GROUP BY BR_CODE,AC_NO
					) AC ON AC.BR_CODE=LM.BR_CODE AND AC.AC_NO=LM.AC_NO
	
	--FOR BALANCE AMOUNT AND REGULAR INTEREST AMT (RI)
	LEFT OUTER JOIN (
		SELECT LM.AC_NO, SUM(CASE WHEN NAMED_AC_CODE = '0401' THEN GD.AMT ELSE 0 END)*-1 BAL
			 ,SUM(CASE WHEN NAMED_AC_CODE = '0402' THEN GD.AMT ELSE 0 END)*-1 RI
		FROM LOAN_MAST LM WITH(NOLOCK)
			INNER JOIN AC_GROUP_GL_MAP MAP WITH(NOLOCK) ON MAP.AC_GROUP_CODE = LM.AC_GROUP_CODE
			INNER JOIN GLTRAN_DETL GD WITH(NOLOCK) ON GD.SOURCE_TRAN_ID = LM.ID AND GD.SOURCE_TYPE_ID=4 AND GD.GL_CODE = MAP.GL_CODE AND GD.BR_CODE = LM.BR_CODE AND GD.VALUE_DATE <= @TRAN_DATE

		WHERE LM.AC_NO = @AC_NO AND LM.BR_CODE = @BR_CODE
		GROUP BY LM.AC_NO
	) AMT ON LM.AC_NO = AMT.AC_NO
	--FOR INSURANCE PREMIUM RATE
	LEFT OUTER JOIN (
		SELECT PM.TRAN_ID, PM.AC_GROUP_CODE , PD.UPTO_MONTH, PD.APPLY_MODE, PM.IS_PREMIUM_PER_ANNUM
		FROM INSURANCE_PREMIUM_RATE_DETL PD WITH(NOLOCK)
			INNER JOIN (
				SELECT TOP 1 M.TRAN_ID, AC_GROUP_CODE, M.IS_PREMIUM_PER_ANNUM
				FROM INSURANCE_PREMIUM_RATE_MAST M WITH(NOLOCK)
				WHERE M.AC_GROUP_CODE = @AC_GROUP_CODE AND M.STATUS = 1 AND M.EFFECT_DATE <= @TRAN_DATE
				ORDER BY TRAN_ID DESC, EFFECT_DATE DESC
			) PM ON PM.TRAN_ID = PD.INSURANCE_PREMIUM_RATE_MAST_ID
		WHERE PM.AC_GROUP_CODE = @AC_GROUP_CODE
	) PREM ON PREM.AC_GROUP_CODE = @AC_GROUP_CODE AND PREM.UPTO_MONTH >= (DATEDIFF(D, LM.LOAN_OPEN_DATE, LM.LOAN_MATURE_DATE)/30)
	WHERE LM.AC_NO = @AC_NO AND LM.AC_GROUP_CODE = @AC_GROUP_CODE AND LM.BR_CODE = @BR_CODE
	ORDER BY PREM.UPTO_MONTH ASC
	
	SELECT L.*,ISNULL(DT.NEP_DATE,'') LOAN_OPEN_DATE_NEP, ISNULL(DT1.NEP_DATE,'') LOAN_MATURE_DATE_NEP, ISNULL(DT2.NEP_DATE,'') SCH_START_DATE_NEP FROM #LOAN L
	LEFT JOIN DATE_TABLE DT ON L.LOAN_OPEN_DATE=DT.ENG_DATE
	LEFT JOIN DATE_TABLE DT1 ON L.LOAN_MATURE_DATE=DT1.ENG_DATE
	LEFT JOIN DATE_TABLE DT2 ON L.SCH_START_DATE=DT2.ENG_DATE

	SELECT DISTINCT AGCHD.CHARGE_CODE,C.CHARGE_NAME,C.GL_CODE GL_CODE,AGCHD1.FLAT_PCT,AGCHD1.CHARGE_VALUE,AGCHD1.TRAN_ID AC_GROUP_CHARGE_HIST_DETL_1_ID,CAST(ROW_NUMBER() OVER (PARTITION BY AGCHD.CHARGE_CODE ORDER BY C.GL_CODE) AS INT) RN 
	FROM AC_GROUP_CHARGE_HIST_DETL AGCHD WITH(NOLOCK)
	INNER JOIN (SELECT AGCHM.TRAN_ID,AGCHM.AC_GROUP_CODE,ROW_NUMBER() OVER(PARTITION BY AGCHM.AC_GROUP_CODE ORDER BY AGCHM.EFFECT_DATE DESC)RN FROM AC_GROUP_CHARGE_HIST_MAST AGCHM WITH(NOLOCK) WHERE AGCHM.EFFECT_DATE<=@TRAN_DATE AND AGCHM.STATUS=1)AGCHM ON AGCHM.TRAN_ID=AGCHD.AC_GROUP_CHARGE_HIST_MAST_ID AND AGCHM.AC_GROUP_CODE=@AC_GROUP_CODE AND AGCHM.RN=1
	INNER JOIN AC_GROUP_CHARGE_HIST_DETL_1 AGCHD1 WITH(NOLOCK) ON AGCHD1.AC_GROUP_CHARGE_HIST_DETL_ID=AGCHD.TRAN_ID
	INNER JOIN CHARGE C WITH(NOLOCK) ON AGCHD.CHARGE_CODE=C.CHARGE_CODE

	DROP TABLE IF EXISTS #LOAN
END
--GO
--EXEC USP_CBS_LoanDisbursement_Get_LoanAccountDetails
--@AC_NO='00130000003',
--@BR_CODE ='001'

