CREATE    PROC [dbo].[USP_CBS_Get_LoginData] 
(
	 @USERNAME VARCHAR(50)		
	,@PASSWORD VARCHAR(200)	
	,@BRANCH_CODE VARCHAR(5)		
)
--WITH ENCRYPTION
AS
BEGIN
	SET NOCOUNT ON;
	SET ARITHABORT ON;
	SET XACT_ABORT ON;
	DECLARE @USERNAME_ VARCHAR(50)=@USERNAME
		,@PASSWORD_ VARCHAR(200)=@PASSWORD
		,@BRANCH_CODE_ VARCHAR(5)=@BRANCH_CODE
	DECLARE @USER_ID VARCHAR(10);
	DECLARE @SYS_DATE DATETIME;
	DECLARE @IS_ADMIN VARCHAR(5)= 'NO';
	DECLARE @HZ_CODE INT = 0;
	DECLARE @GLOBAL_LOGIN_GRACE INT = (SELECT TOP 1 ISNULL(LOGIN_GRACE_CNT,0) FROM GLOBAL_SETUP)
	DECLARE @USER_GROUP_CODE VARCHAR(10) = 0;
	DECLARE @RIGHTS_TRAN INT;
	DECLARE @RIGHTS_EFFECT_DATE DATETIME;
	DECLARE @TILL_DAYS INT;

	SELECT TOP 1 *
	INTO #USERS
	FROM USERS
	WHERE LOGIN_NAME = @USERNAME_
	/* ====================== VALIDATION ====================== */
	--- VALIDATION 0 : CHECK USER STATUS
	IF EXISTS( SELECT TOP 1 'X' FROM #USERS WHERE USER_STATUS_CODE IN ('03', '04', '05') )
	BEGIN
		--RAISERROR('WARNING - ! The User is blocked, please contact the administrator...!!!', 18, 1);
		--RETURN;
		EXEC USP_CBS_Global_Get_ErrorMessage @MSG_CODE ='ERR_USER_BLOCKED'
	END;

	SELECT TOP 1 @USER_ID = [USER_ID], @IS_ADMIN = [ADMIN] FROM #USERS WHERE LOGIN_NAME = @USERNAME_;
	--- VALIDATION 0 : CHECK IF USER IS APPROVED
	IF NOT EXISTS( SELECT TOP 1 'X' FROM #USERS WHERE [USER_ID] = @USER_ID AND APPROVED = 'YES' )
	BEGIN
		--RAISERROR('WARNING - ! User not approved, please contact the administrator...!!!', 18, 1);
		--RETURN;
		EXEC USP_CBS_Global_Get_ErrorMessage @MSG_CODE ='ERR_USER_NOT_APPROVED'
	END;
	--- VALIDATION 0 : CHECK BRANCH EXISTENCE
	IF NOT EXISTS( SELECT TOP 1 'X' FROM BRANCH WHERE BR_CODE = @BRANCH_CODE_ AND APPROVED = 'YES' AND [INTEGRATED]= 'YES' )
	BEGIN
		--RAISERROR('WARNING - ! Invalid Branch...!!!', 18, 1);
		--RETURN;
		EXEC USP_CBS_Global_Get_ErrorMessage @MSG_CODE ='ERR_INVALID_BRANCH'
	END;
	
	-- SET DAY END DATE TO @SYS_DATE
	SELECT TOP 1 @SYS_DATE = [DATE] FROM DAYEND WHERE BR_CODE = @BRANCH_CODE_ ORDER BY TRAN_ID DESC;
	CREATE TABLE #TBL_USER_RIGHT (USER_GROUP_CODE VARCHAR(10), EFFECT_DATE DATE, TILL_DAYS INT, [USER_ID] VARCHAR(10) );
	IF (@IS_ADMIN <> 'YES')
	BEGIN
		; WITH ALIAS AS (
			SELECT DISTINCT ALIAS_USER_ID
			FROM USER_ALIAS
			WHERE OBTAIN_USER_ID = @USER_ID
				AND [STATUS] = 1
				AND @SYS_DATE < DATEADD(DAY, TILL_DAYS, EFFECT_DATE)
		)
		INSERT INTO #TBL_USER_RIGHT(USER_GROUP_CODE, EFFECT_DATE, TILL_DAYS, [USER_ID])
		SELECT USER_GROUP_CODE, EFFECT_DATE, ISNULL(DATEDIFF(DAY, @SYS_DATE, R.EFFECT_TILL_DATE), 0) TILL_DAYS, [USER_ID]
			FROM USER_RIGHTS_ASSIGN_MAST MAST
				INNER JOIN USER_RIGHTS_ASSIGN_BRANCH BR ON MAST.TRAN_ID = BR.USER_RIGHTS_ASSIGN_MAST_ID
				INNER JOIN USER_RIGHTS_ASSIGN_ROLE R ON MAST.TRAN_ID = R.USER_RIGHTS_ASSIGN_MAST_ID
			WHERE MAST.[USER_ID] = @USER_ID AND BR.BR_CODE = @BRANCH_CODE_ AND MAST.[STATUS] = 1
		UNION
		SELECT USER_GROUP_CODE, EFFECT_DATE, 0,  @USER_ID
		FROM (
			SELECT MAST.TRAN_ID, MAST.[USER_ID], UG.USER_GROUP_CODE, EFFECT_TILL_DATE, EFFECT_DATE, BR_CODE
			FROM USER_RIGHTS_ASSIGN_MAST MAST
				INNER JOIN ALIAS ON ALIAS.ALIAS_USER_ID = MAST.[USER_ID]
				INNER JOIN USER_RIGHTS_ASSIGN_BRANCH BR ON MAST.TRAN_ID = BR.USER_RIGHTS_ASSIGN_MAST_ID
				INNER JOIN USER_RIGHTS_ASSIGN_ROLE UG ON MAST.TRAN_ID = UG.USER_RIGHTS_ASSIGN_MAST_ID
			WHERE MAST.[STATUS] = 1 AND BR.BR_CODE = @BRANCH_CODE_
		) UG
		CROSS APPLY (SELECT TOP 1 [DATE] FROM DAYEND WHERE BR_CODE = UG.BR_CODE ORDER BY [DATE] DESC, TRAN_ID DESC) DAYEND
		--- VALIDATION 0 : CHECK IF ASSIGNED TO BRANCH
		IF NOT EXISTS ( SELECT TOP 1 'X' FROM #TBL_USER_RIGHT )
		BEGIN
			--RAISERROR('WARNING - ! User rights not assigned to this branch...!!!', 18, 1);
			--RETURN;
			EXEC USP_CBS_Global_Get_ErrorMessage @MSG_CODE = 'ERR_USER_RIGHTS_NOT_ASSIGNED'
		END;
		--- VALIDATION 0 : CHECK USER RIGHTS DURATION
		IF  NOT EXISTS (
			SELECT 'X' FROM #TBL_USER_RIGHT WHERE TILL_DAYS = 0 OR (DATEADD(DD, TILL_DAYS, @SYS_DATE) > @SYS_DATE)
		)
		BEGIN
			--RAISERROR('WARNING - ! User rights have expired...!!!', 18, 1);
			--RETURN;
			EXEC USP_CBS_Global_Get_ErrorMessage @MSG_CODE = 'ERR_USER_RIGHTS_EXPIRED'
		END;
	END
	SELECT TOP 1 @HZ_CODE = HZ.HZ_CODE 
	FROM BRANCH BR 
		INNER JOIN HR_HOLIDAY_ZONE HZ ON BR.HZ_CODE = HZ.HZ_CODE
	WHERE BR_CODE = @BRANCH_CODE_ AND HZ.ACTIVE = 1 AND HZ.APPROVED = 1;
	--- VALIDATION 0 : CHECK HOLIDAY
	IF( @HZ_CODE = 0 )
	BEGIN
		--RAISERROR('WARNING - ! Invalid Holiday Zone Code...!!!', 18, 1);
		EXEC USP_CBS_Global_Get_ErrorMessage @MSG_CODE = 'ERR_INVALID_HZ_CODE'
	END
	ELSE
	BEGIN
		IF NOT EXISTS( SELECT TOP 1 'X' FROM CALENDAR WHERE HZ_CODE = @HZ_CODE AND DAY_TYPE = 0 AND ENG_DATE = @SYS_DATE )
		BEGIN
			--RAISERROR('WARNING - ! Login not allowed during holidays...!!!', 18, 1);
			--RETURN;
			EXEC USP_CBS_Global_Get_ErrorMessage @MSG_CODE = 'ERR_HOLIDAY_LOGIN'
		END;
	END;
	--VALIDATION 0 : CHECK FIRST LOGIN OF USER
	IF NOT EXISTS(SELECT TOP 1 'X' FROM LOG_HIST LH
				  WHERE LH.USER_ID=@USER_ID
				  UNION 
				  SELECT TOP 1 'X' FROM USER_PW_HIST LH
				  WHERE LH.USER_ID=@USER_ID
				 )
	BEGIN
		--RAISERROR('WARNING ! - Since this is your first login, please change your password before logging in.', 18, 1);
		--RETURN;
		EXEC USP_CBS_Global_Get_ErrorMessage @MSG_CODE = 'ERR_FIRST_LOGIN_PW_CHANGE'
		
	END
	--- VALIDATION 0 : CHECK PASSWORD AGE
	IF NOT EXISTS(
		SELECT TOP 1 'X' FROM GLOBAL_SETUP WHERE PWD_LIFE_IN_DAYS= 0 OR (PWD_LIFE_IN_DAYS >= ISNULL((DATEDIFF(DD, (SELECT TOP 1 EFFECT_DATE FROM USER_PW_HIST WHERE [USER_ID]= @USER_ID AND USER_ID LIKE'%[0-9]%' ORDER BY TRAN_ID DESC), @SYS_DATE)),PWD_LIFE_IN_DAYS))
	)
	BEGIN
		--RAISERROR('WARNING - ! Password age expired. Please change your password before logging in...!!!', 18, 1);
		--RETURN;
		EXEC USP_CBS_Global_Get_ErrorMessage @MSG_CODE = 'ERR_PASSWORD_EXPIRED'
	END;
	--- VALIDATION 0 : ONE TIME PASSWORD
	IF (1 = (SELECT TOP 1 IS_SYS_PW_CREATED FROM USER_PW_HIST WHERE [USER_ID] = @USER_ID ORDER BY TRAN_ID DESC))
	BEGIN
		--RAISERROR('WARNING - ! Please change your password to login...!!!', 18, 1);
		--RETURN;
		EXEC USP_CBS_Global_Get_ErrorMessage @MSG_CODE = 'ERR_PW_CHANGE'
	END
	/*======================== ||SUCCESS|| ====================================*/
	
	DECLARE @HO_BR_CODE VARCHAR(50)
		,@SYS_NDATE VARCHAR(50),@BRANCH_COUNT INT, @IS_HO VARCHAR(5)
	
	SET @HO_BR_CODE=(SELECT TOP 1 BR_CODE FROM BRANCH WHERE HO='YES' AND APPROVED='YES')
	SET @SYS_NDATE=(SELECT TOP 1 NEP_DATE FROM DATE_TABLE WHERE ENG_DATE=@SYS_DATE)
	SET @BRANCH_COUNT=ISNULL((SELECT COUNT('X') FROM BRANCH WHERE INTEGRATED = 'YES'),0)
	SET @IS_HO=(SELECT TOP 1 HO FROM BRANCH WHERE BR_CODE=@BRANCH_CODE_)

		SELECT TOP 1 USERS.[USER_ID]
				, USERS.FULL_NAME
				, USERS.LOGIN_NAME
				, USERS.PWD
				, USERS.[ADMIN]
				, @BRANCH_CODE_ AS BR_CODE 
				, B.BR_NAME AS BR_NAME
				, @HO_BR_CODE AS HO_BR_CODE
				, (CASE WHEN USERS.[ADMIN] = 'YES' THEN '0' ELSE UR.USER_GROUP_CODE END) AS USER_GROUP_CODE
				, CONVERT(VARCHAR(10),@SYS_DATE, 111) AS DAY_END_DATE
				, @SYS_NDATE AS DAY_END_MITI
				, ISNULL(TELLER.TELLER,'NO') TELLER
				, ISNULL(TELLER.HEAD_TELLER,'NO') HEAD_TELLER
				, ISNULL(CUR.CUR_ALIAS, '') AS CUR_ALIAS
				, GS.OPR_DATE_TYPE
				, (CASE WHEN GS.OPR_DATE_TYPE='AD' THEN CONVERT(VARCHAR(10),@SYS_DATE, 111)  ELSE  @SYS_NDATE END) DISPLAY_DATE
				, @IS_HO IS_HO
		FROM #USERS USERS
			 LEFT JOIN #TBL_USER_RIGHT UR ON UR.[USER_ID] = USERS.[USER_ID]
			 OUTER APPLY (SELECT TOP 1 TELLER, HEAD_TELLER, GS.OPR_DATE_TYPE
						FROM TELLER_MAST MAST
							  INNER JOIN TELLER_DETL DETL ON MAST.TRAN_ID = DETL.TRAN_ID AND MAST.BR_CODE = DETL.BR_CODE
							  INNER JOIN GLOBAL_SETUP GS ON GS.LOCAL_CUR_CODE=DETL.CUR_CODE
						WHERE APPROVED='YES' AND MAST.BR_CODE = @BRANCH_CODE_ AND [ACTION] = 1 AND [USER_ID] = @USER_ID AND EFFECT_DATE <= @SYS_DATE
							AND USERS.[USER_ID] = MAST.[USER_ID]
						ORDER BY MAST.TRAN_ID DESC
				) TELLER (TELLER, HEAD_TELLER, OPR_DATE_TYPE)
			 INNER JOIN CURRENCY CUR ON CUR.CUR_CODE = '01'
			 INNER JOIN BRANCH B ON B.BR_CODE=@BRANCH_CODE_
			 INNER JOIN GLOBAL_SETUP GS ON 1=1
		WHERE USERS.[USER_ID]= @USER_ID AND USERS.USER_STATUS_CODE='02' AND USERS.APPROVED='YES'

END
